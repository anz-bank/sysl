# Performs transformations of Sysl models, producing "better" Sysl models.

# Loads and returns a Sysl model.
let empty = '...';
let syslPbPath = '../sysl/sysl.pb';
let syslPb = cond //os.exists(syslPbPath) {
    true: //os.file(syslPbPath),
    false: //net.http.get('https://github.com/anz-bank/sysl/blob/master/pkg/sysl/sysl.pb?raw=true').body,
};
let load = \path
    let syslDescriptor = //encoding.proto.descriptor(syslPb);
    //encoding.proto.decode(syslDescriptor, 'Module' , //os.file(path));

# Returns appName from a node
let rec extractedAppName = \x cond x {
    (:@, :@item): extractedAppName(@item),
    (name: (:part), ...): part, # appName, mixinName
    _: x,
};

# Returns the epName and paramName from a node
let rec extractedEpName = \x cond x {
    (:@, :@item): extractedEpName(@item),
    (name: n, ...): n, # epName, paramName
    _: x,
};

# Extracts the dictionary of annotations of an element.
let annotations = \x x.attrs?:{} where .@ != 'patterns' >> \v v.s?:(v.a.elt >> .s);
# Extracts the set of pattern attributes of an element.
let tags = \x x.attrs?('patterns')?.a.elt:{} => .@item.s;

# Returns a relation of annotations for a given type.
let annoRel = \type \rel
    //rel.union(rel =>
        let t = //dict(.);
        annotations(t(type)) =>
            let names = t where //seq.has_suffix('Name', .@);
            let anno = {$`${type}AnnoName`: .@, $`${type}AnnoValue`: .@value};
            //tuple(names | anno)
    );

# Returns a relation of tags for a given type.
let tagRel = \type \rel
    //rel.union(rel =>
        let t = //dict(.);
        tags(t(type)) =>
            let names = t where //seq.has_suffix('Name', .@);
            //tuple(names | {$`${type}Tag`: .})
    );

# Returns a relation of source contexts for a given type.
let srcRel = \type \rel
    rel =>
        let t = //dict(.);
        let names = t where //seq.has_suffix('Name', .@);
        //tuple(names | {$`${type}Src`: t(type).source_context?:{}})
    ;

# A macro to convert `ret` statement payload string to structured tuples.
let payloadMacro = (
    @grammar: {://grammar.lang.wbnf:
        payload -> (status ("<:" type)? | (status "<:")? type) attr?;
        type -> [^\[\n]*;
        status -> ("ok"|"error"|[1-5][0-9][0-9]);
        attr -> %!Array(nvp|modifier);
        nvp_item -> str | array=%!Array(nvp_item) | dict=%!Dict(nvp_item);
        nvp ->  name=\w+ "=" nvp_item;
        modifier -> "~" name=[\w\+]+;
        str -> ('"' ([^"\\] | [\\][\\brntu'"])* '"' | "'" ([^''])* "'") {
             .wrapRE -> /{()};
        };
        .wrapRE -> /{\s*()\s*};
        .macro Array(child) {
            "[" (child):"," "]"
        }
        .macro Dict(child) {
            "{" entry=(key=child ":" value=child):"," "}"
        }
    :},
    @transform: (payload: //{./util}.simpleTransform),
);

# TODO: this is assuming that attribute values are similar to arrai strings
let rec buildNvp = \nvp cond nvp {
    (array: (nvp_item: i, ...), ...): (a: i => (:.@, @item: buildNvp(.@item))),
    (dict: (entry: i, ...), ...): (d: i => (
        @     : buildNvp(.@item.key.nvp_item),
        @value: buildNvp(.@item.value.nvp_item)
    )),
    (str: ('': s, ...), ...): //eval.value(//seq.join('', s)),
    _: //eval.value(//seq.join('', nvp.''))
};

let (:ranked, ...) = //{./util};

# tries to fix type without full reference
let fixType = \apps \appName \typePayload
    # add trim whitespace function
    let typePayload = //{./util}.trimWhitespace(typePayload);
    let (:app, :type, :field) = //{./util}.unpackType(typePayload);
    let pack = //{./util}.packType;
    cond app {
        (''): pack((app: appName, :type, :field)),
        _: cond {
            (app <: apps): typePayload,
            _: pack((app: appName, type: [app] ++ type, :field))
        }
    }
;

# Converts a return statement payload into a tuple of its parts.
let parseReturn = \payload \apps \currApp
    let ret = //{./util}.invokeMacro(payloadMacro, payload);
    let retType = ret.type?:'';
    ret +> (
        type: fixType(apps, currApp, retType),
        attr: //grammar -> .parse(payloadMacro.@grammar, 'payload', payload).attr?:() -> \attr
                (
                    nvp: attr.nvp?:{} => (@: ranked(.@item.name.''), @value: buildNvp(.@item.nvp_item)),
                    modifier: attr.modifier?:{} => ranked(.@item.name.'')
                )
    )
;

# Replaces all `ret` payloads with structured tuples.
let polishReturns = \m
    let apps = m.apps => .@;
    let tweak = \. \currApp
        cond .ret.payload {
            (:type, ...): ., # Already tweaked.
            _: . +> (ret: .ret +> (payload: parseReturn(.ret.payload, apps, currApp))),
        };

    m +> (apps: m.apps?:{} >>> \currApp \a
        a +> (endpoints: a.endpoints?:{} >> \p
            p +> (stmt: p.stmt?:[] >> \s
                cond s.ret?:{} {{}: s, _: tweak(s, currApp)}
            )
        )
    )
;

# Applies polish functions to the appropriate elements of a Sysl model.
let polish = \m
    polishReturns(m)
    # TODO: More polish.
;

# Returns a normalised, relational model of a Sysl model.
let newNormalize = \m
    let m = polish(m);

    # Applications
    let _apps = m.apps => \(@value:app, ...) (appName: extractedAppName(app), :app);
    let app = _apps => \(:appName, :app)
        (
            :appName,
            appLongName: app.long_name?:{},
            appDocstring: app.docstring?:{},
        );

    # Mixins
    let _mixins = //rel.union(m.apps => \(@value:app, ...)
        app.mixins2?:{} => \mixin (
            appName: extractedAppName(app),
            mixinName: extractedAppName(mixin),
            :mixin,
        )
    ) where .;
    let mixin = _mixins => \(:appName, :mixinName, :mixin)
        (
            :appName,
            :mixinName,
        );

    # Endpoints
    let _eps = //rel.union(m.apps => \(@value:app, ...)
        app.endpoints?:{} where !.@value.is_pubsub?:{} && .@ != empty => \(@:epName, @value:ep)
            (appName: extractedAppName(app), :epName, :ep)
    ) where .;
    let ep = _eps => \(:appName, :epName, :ep)
        (
            :appName,
            :epName,
            epLongName: ep.long_name?:{},
            epDocstring: ep.docstring?:{},
            epEvent: cond ep.source?:{} {
                {}: {},
                s: (appName: s, eventName: //seq.split(' -> ', epName)(1)),
            },
            rest: ep.rest_params?:{},
        );

    # Parameters
    let _params = //rel.union(_eps => \(:appName, :epName, :ep)
        ep.param?:{} => (:appName, :epName, paramName: extractedEpName(.), param: .)
    ) where .;
    let param = _params => \(:appName, :epName, :paramName, param: (:@, @item:param))
        (
            :appName,
            :epName,
            :paramName,
            paramIndex: @,
            paramType: param.type?:{},
        );

    # Statements
    let _stmts = //rel.union(_eps => \(:appName, :epName, :ep)
        ep.stmt?:{} => (:appName, :epName, stmt: .)
    );
    let stmt = (_stmts => \(:appName, :epName, stmt: (@:stmtIndex, @item:stmt))
        let stmtAction = stmt.action?.action?:{};
        cond {
            (stmtAction != empty): (
                :appName,
                :epName,
                :stmtIndex,
                stmtParent: {},
                :stmtAction,
                stmtCall: cond stmt.call?:{} {(:target, :endpoint, ...): (appName: target, epName: endpoint)},
                stmtCond: cond stmt.cond?:{} {(:test, ...): (:test)},
                stmtLoop: cond stmt.loop?:{} {(:mode, :criterion, ...): (:mode, :criterion)},
                stmtLoopN: cond stmt.loop_n?:{} {(:count, ...): (:count)},
                stmtForeach: cond stmt.foreach?:{} {(collection:coll, ...): (:coll)},
                stmtAlt: cond stmt.alt?:{} {(:choice, ...): (:choice)},
                stmtGroup: cond stmt.group?:{} {(:title, ...): (:title)},
                stmtRet: stmt.ret?:{},
            )
        })
        where . != {}
    ;

    # Events
    let _events = //rel.union(m.apps => \(@value:app, ...)
        app.endpoints?:{} where .@value.is_pubsub?:{} => \(@:eventName, @value:event)
            (appName: extractedAppName(app), :eventName, :event)
    ) where .;
    let event = _events => \(:appName, :eventName, :event)
        (
            :appName,
            :eventName,
        );

    # Types
    let _types = //rel.union(m.apps => \(@value:app, ...)
        app.types?:{} => \(@:typeName, @value:type)
            (appName: extractedAppName(app), :typeName, :type)
    ) where .;
    let type = _types => \(:appName, :typeName, :type)
        (
            :appName,
            :typeName,
            typeDocstring: type.docstring?:{},
            typeOpt: type.opt?:{},
        );

    let table = _types where .type.relation?:{} => \(:appName, :typeName, type: (relation: rel, ...), ...)
        (
            :appName,
            :typeName,
            pk: rel.primary_key?.attr_name:{}
        );

    # Fields
    let _fields = //rel.union(m.apps => \(@value:app, ...)
        //rel.union(app.types?:{} => \(@:typeName, @value:type)
            (type.tuple?:(type.relation?:())).attr_defs?:{} => \(@:fieldName, @value:field)
                (appName: extractedAppName(app), :typeName, :fieldName, :field)
        ) where .
    );
    let field = _fields => \(:appName, :typeName, :fieldName, :field)
        (
            :appName,
            :typeName,
            :fieldName,
            fieldOpt: field.opt?:{},
            fieldType: cond field {
                (primitive:p, ...): p,
                (type_ref:(ref:(path:p), ...), ...): //seq.join('.', p),
                _: '?',
            },
            # fk: cond field.type_ref?.ref.path:{} {
            #     [t, f]: (appName:app.name.part, typeName: t, fieldName: f)
            # },
            # pk: !!({'pk'} & tags),
        );

    # View
    let _views = //rel.union(m.apps => \(@value:app, ...)
        app.views?:{} => (appName: extractedAppName(app), viewName: .@, view: .@value)
    ) where .;
    let view = _views => \(:appName, :viewName, :view)
        (
            :appName,
            :viewName,
            viewType: view.ret_type?:{},
        );

    # Build the relations that are common to all types.
    let data = {
        'app': _apps,
        'mixin': _mixins,
        'ep': _eps,
        'stmt': _stmts,
        'event': _events,
        'type': _types,
        'field': _fields,
        'view': _views,
    };
    let [anno, tag, src] = [annoRel, tagRel, srcRel] >> \builder
        //tuple(data >>> \k \v builder(k, v));

    (
        :app,
        :ep,
        :param,
        :stmt,
        :type,
        :table,
        :event,
        :field,
        :view,
        :mixin,
        :anno,
        :tag,
        :src,
    )
;

# Deprecated.
# Returns a normalised, relational model of a Sysl model.
let normalize = \m
    let m = polish(m);
    let apps = m.apps => \(@:appName, @value:app)
        (
            :appName,
            appPackage: app.attrs?('package')?.s:{},
            appDescription: app.attrs?('description')?.s:{},
            appPatterns: tags(app),
            appAnnotations: app.attrs?:{} >>> \k \v v.s?:(v.a.elt >> .s),
        )
    ;
    let endpoints = //rel.union(m.apps => \(@:appName, @value:app)
        app.endpoints?:{} => \(@:epName, ...)
            (
                :appName,
                :epName,
            )
    );
    let calls = //rel.union(m.apps => \(@:appName, @value:app)
        //rel.union(app.endpoints?:{} => \(@:epName, @value:ep)
            ep.stmt?:{} where .@item.call?:{} =>
                (
                    from: (
                        :appName,
                        :epName,
                    ),
                    to: (
                        appName: //seq.join(' :: ', .@item.call.target.part),
                        epName: .@item.call.endpoint,
                    ),
                    # TODO: Use stmt.source_context once populated by parser.
                    # https://github.com/anz-bank/sysl/issues/990
                    sourceContext: ep.source_context +> (:.@),
                )
        )
    );
    let types = //rel.union(m.apps => \(@:appName, @value:app)
        app.types?:{} => \(@:typeName, @value: t)
            (
                :appName,
                :typeName,
                typeAnnotations: t.attrs?:{} >>> \k \v v.s?:(v.a.elt),
                typeSourceContext: t.source_context,
            )
        ) where .
    ;
    let fields = //rel.union(m.apps => \(@:appName, @value:app)
        //rel.union(app.types?:{} => \(@:typeName, @value:type)
            type.relation?.attr_defs?:{} => \(@:fieldName, @value:field)
                let tags = tags(field);
                (
                    :appName,
                    :typeName,
                    :fieldName,
                    fieldType: cond field {
                        (primitive:p, ...): p,
                        (type_ref:(ref:(path:p), ...), ...): //seq.join('.', p),
                        (sequence: (primitive: p, ...), ...): [p],
                        (set: (primitive: p, ...), ...): {p},
                        _: '?',
                    },
                    fieldConstraint: field.constraint?:{},
                    fk: cond field.type_ref?.ref.path:{} {
                        [t, f]: (:appName, typeName: t, fieldName: f)
                    },
                    fieldAnnotations: field.attrs?:{} >>> \k \v v.s?:(v.a.elt >> .s),
                    patterns: tags,
                    pk: 'pk' <: tags,
                    opt: field.opt?:{},
                    fieldSourceContext: field.source_context,
                )
        ) where .
    );
    # let transforms = //{./dfa/transforms}(m.apps);
    (
        :apps,
        :endpoints,
        :calls,
        :types,
        :fields,
        # :transforms,
        # TODO:
        # :annotations, (aka attributes)
        # :tags, (aka patterns)
    )
;

# Export the functions from the module.
(
    :load,
    :polish,
    :normalize,
    :newNormalize,

    :extractedAppName,
    :extractedEpName,
    :annotations,
    :tags,
    :annoRel,
    :tagRel,
    :parseReturn,
)
