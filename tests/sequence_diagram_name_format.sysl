User [~human]:
  Check Balance:
    MobileApp <- Login
    MobileApp <- Check Balance

MobileApp [~ui]:
  Login:
    Server <- Login
  Check Balance:
    Server <- Read User Balance

Server:
  Login:
    do input validation
    DB <- Save
    AuditTopic <- Publish
    return success or failure

  Read User Balance:
    DB <- Load
    Statement <- Download
    return ok <: balance

AuditTopic [~topic]:
    <-> Publish: ...

Statement [~file]:
    Download:
        return ok <: pdf

DB [~db]:
  Save: ...
  Load: ...

Server [status="modified"]:
  .. * <- *:
    DB <- Save [status="modified"]
    DB <- Load [status="modified"]

Project [seqtitle="Diagram", appfmt="%(@status?<color red>%(appname)</color>|%(appname))", epfmt="%(@status? <color green>%(epname)</color>|%(epname))"]:
  Seq:
    User <- Check Balance
